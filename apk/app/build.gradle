apply from: '../versions.gradle'
apply plugin: 'com.android.application'

//Crashlytics
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
//Crashlytics end

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "com.fgl27.twitch"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.publishVersionCode
        versionName versions.publishVersion + versions.publishVersionCode
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles = [
                    getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            ]
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        lintConfig file("lint.xml")
        disable 'SetJavaScriptEnabled', //To run webview and load js file
                'GoogleAppIndexingWarning', //GoogleAppIndexingWarning: Missing support for Firebase App Indexing
                'IconMissingDensityFolder', //IconMissingDensityFolder: Missing density folder
                'IconLauncherShape', //IconLauncherShape: Looks better the way it's
                'RtlHardcoded', //Not suporting it
                'ObsoleteLintCustomCheck' //Firebase related ignore
    }
    productFlavors {
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    //Exoplayer
    implementation project(':exoplayer-library-core')
    implementation project(':exoplayer-library-ui')
    implementation project(':exoplayer-library-hls')

    //Crashlytics
    implementation platform('com.google.firebase:firebase-bom:' + versions.firebase_bom)
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    //Crashlytics end

    implementation 'androidx.leanback:leanback:' + versions.leanback
    implementation 'androidx.webkit:webkit:' + versions.webkit
    implementation 'com.google.code.gson:gson:' + versions.googlegson
    implementation 'net.grandcentrix.tray:tray:' + versions.tray
    implementation 'androidx.tvprovider:tvprovider:' + versions.tvprovider

}
